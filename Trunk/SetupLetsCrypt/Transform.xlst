<?xml version="1.0" encoding="iso-8859-1"?>
<xsl:stylesheet version="1.0"
                xmlns:xsl="http://www.w3.org/1999/XSL/Transform"
                xmlns:wix="http://schemas.microsoft.com/wix/2006/wi"
                xmlns="http://schemas.microsoft.com/wix/2006/wi"
                exclude-result-prefixes="xsl wix">

	<xsl:output method="xml" indent="yes" omit-xml-declaration="yes" />

	<xsl:strip-space elements="*"/>

	<xsl:key name="ElementsToRemove" 
			 match="wix:Component[contains(wix:File/@Source, '.exe') or contains(wix:File/@Source, '.json') or contains(wix:File/@Source, '.config')]" use="@Id" />

	<xsl:template match="*[self::wix:Component or self::wix:ComponentRef] [key('ElementsToRemove', @Id)]" />

	<!-- ### Adding the Win64-attribute to all Components -->
	<xsl:template match="wix:Component">
		<xsl:copy>
			<xsl:apply-templates select="@*" />
			<xsl:attribute name="Win64">yes</xsl:attribute>
			<xsl:apply-templates select="node()" />
		</xsl:copy>
	</xsl:template>

	<xsl:template match="@*|node()">
		<xsl:copy>
			<xsl:apply-templates select="@*|node()"/>
		</xsl:copy>
	</xsl:template>
	
</xsl:stylesheet>